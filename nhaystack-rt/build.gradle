//  Copyright (c) 2012, J2 Innovations
//  Licensed under the Academic Free License version 3.0
// 
//  History:
//    07 Nov 2011  Richard McElhinney  Creation
//    28 Sep 2012  Mike Jarmy          Ported from axhaystack
//    31 May 2016  Richard McElhinney  Ported from nhaystack for Niagara 4

apply from: "${System.getenv("niagara_home")}/etc/gradle/niagara.gradle"
apply from: "${System.getenv("niagara_home")}/etc/gradle/idea.gradle"

ext {
  name = 'nhaystack'
  project.version = '2.0.2'
  project.group = 'Project Haystack'
  description = "Niagara Haystack"
}

niagaraModule {
  preferredSymbol = "nh"
  runtimeProfile = "rt"
  moduleName = "nhaystack"
  
  modulePart {
    name = "nhaystack-wb"
	runtimeProfile = "wb"
  }
}

repositories { jcenter() }

dependencies {
  compile fileTree(dir: 'lib', include: ['*.jar'])

  compile "Tridium:nre:4.0.22"
  compile "Tridium:alarm-rt:4.0.22"
  compile "Tridium:baja:4.0.22"
  compile "Tridium:bql-rt:4.0.22"
  compile "Tridium:control-rt:4.0.22"
  compile "Tridium:driver-rt:4.0.22"
  compile "Tridium:driver-wb:4.0.22"
  compile "Tridium:file-rt:4.0.22"
  compile "Tridium:fox-rt:4.0.22"
  compile "Tridium:gx-rt:4.0.22"
  compile "Tridium:history-rt:4.0.22"
  compile "Tridium:net-rt:4.0.22"
  compile "Tridium:niagaraDriver-rt:4.0.22"
  compile "Tridium:schedule-rt:4.0.22"
  compile "Tridium:web-rt:4.0.22"

  compile "Tridium:bajaui-wb:4.0.22"
  compile "Tridium:workbench-wb:4.0.22"

  testCompile 'org.testng:testng:6.10'
  testCompile 'Tridium:alarm-rt:4.0.22'
  testCompile 'Tridium:test-se:4.0.0'
//  testCompile 'org.mockito:mockito-core:2.+'
  testCompile fileTree(dir: 'lib', include: ['*.jar'])

  uberjar "javax.servlet:javax.servlet-api:3.0.1"
}

test {
  useTestNG()

  include 'nhaystack/test/**'
  include 'nhaystack/server/**'

  testLogging.showStandardStreams = true
}

jar {
  from('src') {
    include 'nhaystack/res/*.csv'
    include 'nhaystack/res/*.txt'
    include 'nhaystack/res/equip-points/*.txt'
    include 'nhaystack/icons/*.*'
  }    
  from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) 
  {
      include "org/projecthaystack/**"     
  }
}